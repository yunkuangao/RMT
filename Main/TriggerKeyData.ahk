#Requires AutoHotkey v2.0

class TriggerKeyData {
    __New(Key) {
        this.Key := Key
        this.OriDownArr := []  ;按下触发
        this.OriLoosenArr := []    ;松开触发
        this.OriLoosenStopArr := []    ;松止
        this.OriTogArr := []   ;开关
        this.OriHoldArr := []  ;按长按
        this.HoldActionMap := Map()

        this.DownArr := []
        this.LoosenArr := []
        this.LoosenStopArr := []
        this.TogArr := []
        this.HoldArr := []

        this.InitState()
    }

    InitState() {
        this.IsSoftHotKey := false
        for index, value in MySoftData.SoftHotKeyArr {
            key := LTrim(value, "~")
            key := StrLower(key)
            if (this.Key == key) {
                this.IsSoftHotKey := true
                break
            }
        }
    }

    AddData(info) {
        TriggerType := info.GetTriggerType()
        if (TriggerType == 1)
            this.OriDownArr.Push(info)
        if (TriggerType == 2)
            this.OriLoosenArr.Push(info)
        if (TriggerType == 3)
            this.OriLoosenStopArr.Push(info)
        if (TriggerType == 4)
            this.OriTogArr.Push(info)
        if (TriggerType == 5)
            this.OriHoldArr.Push(info)
    }

    UpdataArr() {
        this.DownArr := []
        this.LoosenArr := []
        this.LoosenStopArr := []
        this.TogArr := []
        this.HoldArr := []

        MyMouseInfo.UpdateInfo()
        this.UpdateArrByFront(this.OriDownArr, this.DownArr)
        this.UpdateArrByFront(this.OriLoosenArr, this.LoosenArr)
        this.UpdateArrByFront(this.OriLoosenStopArr, this.LoosenStopArr)
        this.UpdateArrByFront(this.OriTogArr, this.TogArr)
        this.UpdateArrByFront(this.OriHoldArr, this.HoldArr)
    }

    UpdateArrByFront(OriArr, ResArr) {
        tableItem := MySoftData.TableInfo[1]
        for index, value in OriArr {
            infoStr := value.GetFrontStr()
            if (infoStr == "")
                continue

            if (MyMouseInfo.CheckIfMatch(infoStr, false))
                ResArr.Push(value)
        }

        ; 如果没有找到任何符合条件的窗口
        if (ResArr.Length == 0) {
            for index, value in OriArr {
                if (value.GetFrontStr() == "")
                    ResArr.Push(value)
            }
        }
    }

    OnTriggerKeyDown() {
        this.UpdataArr()
        this.HandleSoftHotKeyDown()
        for index, value in this.DownArr {
            if (index == 1 && SubStr(value.GetTK(), 1, 1) != "~")
                LoosenModifyKey(value.GetTK())

            value.Action()
        }

        for index, value in this.LoosenStopArr {
            value.Action()
        }

        for index, value in this.TogArr {
            value.Action()
        }

        this.SetHoldTimeChecker()
    }

    OnTriggerKeyUp() {
        this.UpdataArr()
        this.HandleSoftHotKeyUp()
        tableItem := MySoftData.TableInfo[1]
        for index, value in this.LoosenArr {
            value.Action()
        }

        for index, value in this.LoosenStopArr {
            value.CancelAction()
        }

        this.DelHoldTimeChecker()
    }

    SetHoldTimeChecker() {
        tableItem := MySoftData.TableInfo[1]
        for _, value in this.HoldArr {
            isWork := value.GetWorkState()
            if (isWork)
                continue
            if (this.HoldActionMap.Has(value))
                continue
            holdTime := value.GetHoldTime()
            action := this.HoldTimeAction.Bind(this, value)
            SetTimer(action, -holdTime)
            this.HoldActionMap.Set(value, action)
        }
    }

    DelHoldTimeChecker() {
        for key, value in this.HoldActionMap {
            SetTimer(value, 0)
        }
        this.HoldActionMap := Map()
    }

    HoldTimeAction(info) {
        keyCombo := LTrim(info.GetTK(), "~")
        if (this.HoldActionMap.Has(info))
            this.HoldActionMap.Delete(info)

        if (AreKeysPressed(keyCombo))
            info.Action()
    }

    HandleSoftHotKeyDown() {
        if (!this.IsSoftHotKey)
            return

        if (this.Key == "wheelup" || this.Key == "wheeldown") {
            if (MyMouseInfo.CheckIfMatch("RMTv⎖⎖")) {
                MySlider.OnScrollWheel(this.Key)
            }

            if (MyMouseInfo.CheckIfMatch("RMT-FreePaste⎖⎖")) {
                MyFreePasteGui.OnScrollWheel(this.Key)
            }
        }

        isArrowKey1 := this.Key == "left" || this.Key == "right"
        isArrowKey2 := this.Key == "up" || this.Key == "down"
        isArrowKey := isArrowKey1 || isArrowKey2
        if (isArrowKey) {
            MyTargetGui.OnArrowKeyDown(this.Key)
        }

        if (this.Key == "lbutton") {
            if (MySoftData.SelectAreaAction != "") {
                SelectArea()
            }

            if (MySoftData.GetAreaAction != "") {
                OnGetSelectAreaDown(this.Key)
            }
        }
    }

    HandleSoftHotKeyUp() {
        if (!this.IsSoftHotKey)
            return

        if (this.Key == "lbutton") {
            if (MyMouseInfo.CheckIfMatch("RMT-Target⎖⎖")) {
                MyTargetGui.OnLButtonUp(this.Key)
            }

            if (MySoftData.GetAreaAction != "") {
                OnGetSelectAreaUp(this.Key)
            }
        }

        if (this.Key == "enter") {
            MyTargetGui.OnEnterUp(this.Key)
        }
    }
}

class TriggerKeyInfo {
    __New() {
        this.macroType := 1     ; 1:item 2:fold
        this.tableIndex := 1    ;table索引
        this.itemIndex := 1     ;item索引
        this.foldIndex := 1     ;折叠框索引
    }

    GetFrontStr() {
        tableItem := MySoftData.TableInfo[this.tableIndex]
        if (this.macroType == 1)
            return GetItemFrontInfo(tableItem, this.tableIndex)
        else if (this.macroType == 2) {
            return tableItem.FoldInfo.FrontInfoArr[this.foldIndex]
        }
        return ""
    }

    GetTK() {
        tableItem := MySoftData.TableInfo[this.tableIndex]
        if (this.macroType == 1)
            return tableItem.TKArr[this.itemIndex]
        else if (this.macroType == 2) {
            return tableItem.FoldInfo.TKArr[this.foldIndex]
        }
        return 1
    }

    GetTriggerType() {      ;触发类型   "按下", "松开", "松止", "开关", "长按"
        tableItem := MySoftData.TableInfo[this.tableIndex]
        if (this.macroType == 1)
            return tableItem.TriggerTypeArr[this.itemIndex]
        else if (this.macroType == 2) {
            return tableItem.FoldInfo.TKTypeArr[this.foldIndex]
        }
        return 1
    }

    GetHoldTime() {
        tableItem := MySoftData.TableInfo[this.tableIndex]
        if (this.macroType == 1)
            return tableItem.HoldTimeArr[this.itemIndex]
        else if (this.macroType == 2) {
            return tableItem.FoldInfo.HoldTimeArr[this.foldIndex]
        }
        return 500
    }

    GetWorkState() {
        tableItem := MySoftData.TableInfo[this.tableIndex]
        if (this.macroType == 1) {
            return tableItem.IsWorkIndexArr[this.itemIndex]
        }
        else {
            return MySoftData.CurMenuWheelIndex == this.foldIndex
        }
    }

    Action() {
        tableItem := MySoftData.TableInfo[this.tableIndex]
        triggerType := this.GetTriggerType()
        if (this.macroType == 1) {
            if (triggerType == 4) {
                isWork := tableItem.IsWorkIndexArr[this.itemIndex]
                if (isWork) {       ;关闭开关
                    MySubMacroStopAction(this.tableIndex, this.itemIndex)
                    return
                }
                OnToggleTriggerMacro(this.tableIndex, this.itemIndex)
            }
            else
                TriggerMacroHandler(this.tableIndex, this.itemIndex)
        }
        else {
            OpenMenuWheel(this.foldIndex)
        }
    }

    CancelAction() {
        tableItem := MySoftData.TableInfo[this.tableIndex]
        triggerType := this.GetTriggerType()
        if (this.macroType == 1) {
            if (triggerType == 3) {
                isWork := tableItem.IsWorkIndexArr[this.itemIndex]
                if (isWork) {
                    workPath := MyWorkPool.GetWorkPath(tableItem.IsWorkIndexArr[this.itemIndex])
                    MyWorkPool.PostMessage(WM_STOP_MACRO, workPath, 0, 0)
                    return
                }
                KillTableItemMacro(tableItem, this.itemIndex)
            }
        }
        else {
            CloseMenuWheel()
        }
    }
}
