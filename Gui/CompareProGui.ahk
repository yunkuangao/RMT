#Requires AutoHotkey v2.0
#Include CompareProEditItemGui.ahk

class CompareProGui {
    __new() {
        this.ParentTile := ""
        this.Gui := ""
        this.SureBtnAction := ""
        this.RemarkCon := ""
        this.MacroGui := ""
        this.VariableObjArr := []
        this.FocusCon := ""
        this.ItemEditGui := ""
        this.ContextMenu := ""

        this.CompareTypeStrArr := GetLangArr(["å¤§äºŽ", "å¤§äºŽç­‰äºŽ", "ç­‰äºŽ", "å°äºŽç­‰äºŽ",
            "å°äºŽ", "å­—ç¬¦åŒ…å«", "å˜é‡å­˜åœ¨"])

        this.CompareTypeStrMap := Map(GetLang("å¤§äºŽ"), 1, GetLang("å¤§äºŽç­‰äºŽ"), 2, GetLang("ç­‰äºŽ"), 3, GetLang("å°äºŽç­‰äºŽ"),
        4, GetLang("å°äºŽ"), 5, GetLang("å­—ç¬¦åŒ…å«"), 6, GetLang("å˜é‡å­˜åœ¨"), 7)

        this.Data := ""
    }

    ShowGui(cmd) {
        if (this.Gui != "") {
            this.Gui.Show()
        }
        else {
            this.AddGui()
        }

        this.Init(cmd)
        this.ToggleFunc(true)
    }

    AddGui() {
        MyGui := Gui(, this.ParentTile GetLang("å¦‚æžœProç¼–è¾‘å™¨"))
        this.Gui := MyGui
        MyGui.SetFont("S10 W550 Q2", MySoftData.FontType)

        PosX := 10
        PosY := 10
        MyGui.Add("Text", Format("x{} y{}", PosX, PosY), GetLang("å¿«æ·æ–¹å¼:"))
        PosX += 70
        con := MyGui.Add("Hotkey", Format("x{} y{} w{}", PosX, PosY - 3, 70), "!l")
        con.Enabled := false

        PosX += 90
        btnCon := MyGui.Add("Button", Format("x{} y{} w{}", PosX, PosY - 5, 80), GetLang("æ‰§è¡ŒæŒ‡ä»¤"))
        btnCon.OnEvent("Click", (*) => this.TriggerMacro())

        PosX += 90
        MyGui.Add("Text", Format("x{} y{} w{}", PosX, PosY, 50), GetLang("å¤‡æ³¨:"))
        PosX += 50
        this.RemarkCon := MyGui.Add("Edit", Format("x{} y{} w{}", PosX, PosY - 5, 150), "")

        PosX := 10
        PosY += 30
        this.LVCon := MyGui.Add("ListView", Format("x{} y{} w480 h280 -LV0x10 NoSort", PosX, PosY), GetLangArr(["æ¡ä»¶",
            "å…³ç³»", "æŒ‡ä»¤"]))
        this.LVCon.OnEvent("ContextMenu", this.ShowContextMenu.Bind(this))
        this.LVCon.OnEvent("DoubleClick", this.OnDoubleClick.Bind(this))
        ; è®¾ç½®åˆ—å®½ï¼ˆå•ä½ï¼špxï¼‰
        this.LVCon.ModifyCol(1, 260) ; ç¬¬ä¸€åˆ—å®½åº¦
        this.LVCon.ModifyCol(2, 50) ; è‡ªåŠ¨å¡«å……å‰©ä½™å®½åº¦
        this.LVCon.ModifyCol(3, 150) ; è‡ªåŠ¨å¡«å……å‰©ä½™å®½åº¦

        PosY += 290
        PosX := 190
        btnCon := MyGui.Add("Button", Format("x{} y{} w{} h{}", PosX, PosY, 100, 40), GetLang("ç¡®å®š"))
        btnCon.OnEvent("Click", (*) => this.OnClickSureBtn())
        this.FocusCon := btnCon

        MyGui.OnEvent("Close", (*) => this.ToggleFunc(false))
        MyGui.Show(Format("w{} h{}", 500, 380))
    }

    Init(cmd) {
        cmdArr := cmd != "" ? StrSplit(cmd, "_") : []
        this.SerialStr := cmdArr.Length >= 2 ? cmdArr[2] : GetSerialStr("ComparePro")
        this.RemarkCon.Value := cmdArr.Length >= 3 ? cmdArr[3] : ""
        this.Data := this.GetCompareProData(this.SerialStr)

        this.LVCon.Delete()
        loop this.Data.MacroArr.Length {
            condiStr := ""
            ItemIndex := A_Index
            loop this.Data.VariNameArr[ItemIndex].Length {
                condiStr .= this.Data.VariNameArr[ItemIndex][A_Index] " " this.CompareTypeStrArr[this.Data.CompareTypeArr[
                    ItemIndex][A_Index]] " " this.Data.VariableArr[ItemIndex][A_Index]
                condiStr .= "âŽ–"
            }
            condiStr := Trim(condiStr, "âŽ–")
            logicStr := this.Data.LogicTypeArr[A_Index] == 1 ? GetLang("ä¸”") : GetLang("æˆ–")
            macro := this.Data.MacroArr[A_Index]

            this.LVCon.Add(, condiStr, logicStr, macro)
        }
        this.LVCon.Add(, GetLang("ä»¥ä¸Šéƒ½ä¸æ˜¯"), "", this.Data.DefaultMacro)
        this.LVCon.Focus()  ; ðŸ”¥ å¼ºåˆ¶èŽ·å¾—ç„¦ç‚¹ï¼Œè§£å†³ç¬¬ä¸€æ¬¡åŒå‡»æ— æ•ˆé—®é¢˜
    }

    ToggleFunc(state) {
        MacroAction := (*) => this.TriggerMacro()
        if (state) {
            Hotkey("!l", MacroAction, "On")
        }
        else {
            Hotkey("!l", MacroAction, "Off")
        }
    }

    ShowContextMenu(ctrl, item, isRightClick, x, y) {
        if (item == 0)
            return

        if (this.ContextMenu == "") {
            this.ContextMenu := Menu()
            this.ContextMenu.Add(GetLang("ç¼–è¾‘"), (*) => this.MenuHandler(GetLang("ç¼–è¾‘")))
            this.ContextMenu.Add()  ; åˆ†éš”çº¿
            this.ContextMenu.Add(GetLang("å‘ä¸Šæ’å…¥åˆ†æ”¯"), (*) => this.MenuHandler(GetLang("å‘ä¸Šæ’å…¥åˆ†æ”¯")))
            this.ContextMenu.Add(GetLang("å‘ä¸‹æ’å…¥åˆ†æ”¯"), (*) => this.MenuHandler(GetLang("å‘ä¸‹æ’å…¥åˆ†æ”¯")))
            this.ContextMenu.Add()  ; åˆ†éš”çº¿
            this.ContextMenu.Add(GetLang("å‘ä¸Šç§»åŠ¨"), (*) => this.MenuHandler(GetLang("å‘ä¸Šç§»åŠ¨")))
            this.ContextMenu.Add(GetLang("å‘ä¸‹ç§»åŠ¨"), (*) => this.MenuHandler(GetLang("å‘ä¸‹ç§»åŠ¨")))
            this.ContextMenu.Add()  ; åˆ†éš”çº¿
            this.ContextMenu.Add(GetLang("åˆ é™¤"), (*) => this.MenuHandler(GetLang("åˆ é™¤")))
        }
        this.CurItme := item
        this.ContextMenu.Show(x, y)
    }

    OnDoubleClick(ctrl, item) {
        if (item == 0)
            return
        this.OnEditItem(item)
    }

    MenuHandler(cmdStr) {
        isFinally := this.LVCon.GetText(this.CurItme, 1) == GetLang("ä»¥ä¸Šéƒ½ä¸æ˜¯")
        switch cmdStr {
            case GetLang("ç¼–è¾‘"):
            {
                this.OnEditItem(this.CurItme)
            }
            case GetLang("å‘ä¸Šæ’å…¥åˆ†æ”¯"):
            {
                this.LVCon.Insert(this.CurItme, , GetLang("Num1 å¤§äºŽ Num1"), GetLang("ä¸”"), "")
            }
            case GetLang("å‘ä¸‹æ’å…¥åˆ†æ”¯"):
            {
                if (isFinally) {
                    MsgBox(GetLang("ä¸å¯å‘æœ€åŽçš„åˆ†æ”¯æ’å…¥"))
                    return
                }
                this.LVCon.Insert(this.CurItme + 1, , GetLang("Num1 å¤§äºŽ Num1"), GetLang("ä¸”"), "")
            }
            case GetLang("å‘ä¸Šç§»åŠ¨"):
            {
                if (isFinally) {
                    MsgBox(GetLang("æœ€åŽçš„åˆ†æ”¯ä¸èƒ½å˜æ›´é¡ºåº"))
                    return
                }
                if (this.CurItme == 1) {
                    MsgBox(GetLang("ç¬¬ä¸€ä¸ªåˆ†æ”¯ä¸èƒ½ä¸Šç§»"))
                    return
                }
                this.LVCon.Insert(this.CurItme - 1, , this.LVCon.GetText(this.CurItme, 1), this.LVCon.GetText(this.CurItme,
                    2), this.LVCon.GetText(this.CurItme, 3))
                this.LVCon.Delete(this.CurItme + 1)
            }
            case GetLang("å‘ä¸‹ç§»åŠ¨"):
            {
                if (isFinally || this.LVCon.GetCount() == this.CurItme + 1) {
                    MsgBox(GetLang("æœ€åŽçš„åˆ†æ”¯ä¸èƒ½å˜æ›´é¡ºåº"))
                    return
                }

                this.LVCon.Insert(this.CurItme + 2, , this.LVCon.GetText(this.CurItme, 1), this.LVCon.GetText(this.CurItme,
                    2), this.LVCon.GetText(this.CurItme, 3))
                this.LVCon.Delete(this.CurItme)
            }
            case GetLang("åˆ é™¤"):
            {
                if (isFinally) {
                    MsgBox(GetLang("æœ€åŽçš„åˆ†æ”¯ä¸èƒ½åˆ é™¤ï¼Œè‹¥æ— éœ€è¯¥åˆ†æ”¯è¯·æ¸…ç©ºåˆ†æ”¯æŒ‡ä»¤"))
                    return
                }
                this.LVCon.Delete(this.CurItme)
            }
        }
    }

    OnEditItem(item) {
        if (this.ItemEditGui == "") {
            this.ItemEditGui := CompareProEditItemGui()
            this.ItemEditGui.SureFocusCon := this.FocusCon
        }
        ParentTile := StrReplace(this.Gui.Title, GetLang("ç¼–è¾‘å™¨"), "")
        this.ItemEditGui.ParentTile := ParentTile "-"

        this.ItemEditGui.VariableObjArr := this.VariableObjArr
        EditType := this.LVCon.GetText(item, 1) == GetLang("ä»¥ä¸Šéƒ½ä¸æ˜¯") ? 2 : 1
        DataArr := this.GetCondiStrDataArr(this.LVCon.GetText(item, 1))
        logicStr := this.LVCon.GetText(item, 2)
        macro := this.LVCon.GetText(item, 3)
        this.ItemEditGui.ShowGui(EditType, DataArr, logicStr, macro)
        this.ItemEditGui.SureBtnAction := this.OnSureEditItem.Bind(this, item)
    }

    OnSureEditItem(item, condiStr, logicStr, macro) {
        this.LVCon.Modify(item, , condiStr, logicStr, macro)
    }

    OnClickSureBtn() {
        valid := this.CheckIfValid()
        if (!valid)
            return
        this.SaveCompareProData()
        this.ToggleFunc(false)
        CommandStr := this.GetCommandStr()
        action := this.SureBtnAction
        action(CommandStr)
        this.Gui.Hide()
    }

    CheckIfValid() {
        return true
    }

    TriggerMacro() {
        this.SaveCompareProData()
        CommandStr := this.GetCommandStr()
        tableItem := MySoftData.SpecialTableItem
        tableItem.KilledArr[1] := false
        tableItem.PauseArr[1] := 0
        tableItem.ActionCount[1] := 0
        tableItem.VariableMapArr[1] := Map()
        tableItem.index := 1

        OnComparePro(tableItem, CommandStr, 1)
    }

    GetCommandStr() {
        hasRemark := this.RemarkCon.Value != ""
        CommandStr := Format("{}_{}", GetLang("å¦‚æžœPro"), this.Data.SerialStr)
        if (hasRemark) {
            CommandStr .= "_" this.RemarkCon.Value
        }

        return CommandStr
    }

    GetCompareProData(SerialStr) {
        saveStr := IniRead(CompareProFile, IniSection, SerialStr, "")
        if (!saveStr) {
            data := CompareProData()
            data.SerialStr := SerialStr
            return data
        }

        data := JSON.parse(saveStr, , false)
        return data
    }

    GetCondiStrDataArr(condiStr) {
        condiStrArr := StrSplit(condiStr, "âŽ–")
        VariNameArr := []
        CompareTypeArr := []
        VariableArr := []
        if (condiStr != GetLang("ä»¥ä¸Šéƒ½ä¸æ˜¯")) {
            loop condiStrArr.Length {
                itemCondiArr := StrSplit(condiStrArr[A_Index], " ")
                Variable := itemCondiArr.Length >= 3 ? itemCondiArr[3] : ""
                VariNameArr.Push(itemCondiArr[1])
                CompareTypeArr.Push(this.CompareTypeStrMap[itemCondiArr[2]])
                VariableArr.Push(Variable)
            }
        }

        return [VariNameArr, CompareTypeArr, VariableArr]
    }

    SaveCompareProData() {
        this.Data.VariNameArr := []
        this.Data.CompareTypeArr := []
        this.Data.VariableArr := []
        this.Data.LogicTypeArr := []
        this.Data.MacroArr := []
        loop this.LVCon.GetCount() {
            if (A_Index == this.LVCon.GetCount()) {
                this.Data.DefaultMacro := this.LVCon.GetText(A_Index, 3)
                break
            }
            CondiDataArr := this.GetCondiStrDataArr(this.LVCon.GetText(A_Index, 1))
            LogicType := this.LVCon.GetText(A_Index, 2) == GetLang("ä¸”") ? 1 : 2
            this.Data.VariNameArr.Push(CondiDataArr[1])
            this.Data.CompareTypeArr.Push(CondiDataArr[2])
            this.Data.VariableArr.Push(CondiDataArr[3])
            this.Data.LogicTypeArr.Push(LogicType)
            this.Data.MacroArr.Push(this.LVCon.GetText(A_Index, 3))
        }

        saveStr := JSON.stringify(this.Data, 0)
        IniWrite(saveStr, CompareProFile, IniSection, this.Data.SerialStr)
        if (MySoftData.DataCacheMap.Has(this.Data.SerialStr)) {
            MySoftData.DataCacheMap.Delete(this.Data.SerialStr)
        }
    }
}
