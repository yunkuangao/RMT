#Requires AutoHotkey v2.0

class VariableListenGui {
    __new() {
        this.Gui := ""
        this.TopCon := ""
        this.LVCon := ""
    }

    ShowGui() {
        if (this.Gui != "") {
            this.Gui.Show()
        }
        else {
            this.AddGui()
        }
        IniWrite(true, IniFile, IniSection, "IsOpenListenVar")
        this.Refresh()
        this.LVCon.Focus()  ; ğŸ”¥ å¼ºåˆ¶è·å¾—ç„¦ç‚¹ï¼Œè§£å†³ç¬¬ä¸€æ¬¡åŒå‡»æ— æ•ˆé—®é¢˜
    }

    Refresh() {
        if (!IsObject(this.Gui))
            return

        style := WinGetStyle(this.Gui)
        isVisible := (style & 0x10000000)  ; 0x10000000 = WS_VISIBLE
        if (!isVisible)
            return

        this.LVCon.Opt("-Redraw")
        count := this.LVCon.GetCount()
        LVKeys := Map()
        loop count {
            row := count - A_Index + 1
            key := this.LVCon.GetText(row, 1)
            value := this.LVCon.GetText(row, 2)
            if !MySoftData.VariableMap.Has(key)
                this.LVCon.Delete(row)
            else if (String(MySoftData.VariableMap[key]) != value)
                this.LVCon.Delete(row)
            else
                LVKeys[key] := True
        }

        ; 3) æ·»åŠ  Map ä¸­æœ‰ä½† LV æ²¡æœ‰çš„é¡¹
        for key, value in MySoftData.VariableMap {
            if !LVKeys.Has(key) {
                this.LVCon.Add(, key, value)
            }
        }
        this.LVCon.Opt("+Redraw")
    }

    AddGui() {
        MyGui := Gui(, GetLang("å˜é‡ç›‘è§†å™¨"))
        this.Gui := MyGui
        MyGui.SetFont("S11 W550 Q2", MySoftData.FontType)

        PosX := 10
        PosY := 10
        this.TopCon := MyGui.Add("Checkbox", Format("x{} y{}", PosX, PosY), GetLang("çª—å£ç½®é¡¶"))
        this.TopCon.OnEvent("Click", this.OnTogTop.Bind(this))

        PosX := 10
        PosY += 30
        this.LVCon := MyGui.Add("ListView", Format("x{} y{} w350 h250 -LV0x10 NoSort Sort", PosX, PosY), GetLangArr([
            "å˜é‡å", "å˜é‡å€¼"]))
        ; è®¾ç½®åˆ—å®½ï¼ˆå•ä½ï¼špxï¼‰
        this.LVCon.ModifyCol(1, 120) ; ç¬¬ä¸€åˆ—å®½åº¦
        this.LVCon.ModifyCol(2, 205) ; è‡ªåŠ¨å¡«å……å‰©ä½™å®½åº¦
        this.LVCon.OnEvent("DoubleClick", this.OnDoubleClick.Bind(this))

        MyGui.OnEvent("Close", this.OnClose.Bind(this))
        MyGui.Show(Format("w{} h{}", 370, 300))
    }

    OnClose(*) {
        IniWrite(false, IniFile, IniSection, "IsOpenListenVar")
    }

    OnTogTop(*) {
        state := this.topCon.Value
        if (state) {
            this.Gui.Opt("+AlwaysOnTop")
        }
        else {
            this.Gui.Opt("-AlwaysOnTop")
        }
    }

    OnDoubleClick(LV, RowNumber, *) {
        varName := this.LVCon.GetText(RowNumber, 1)
        curValue := this.LVCon.GetText(RowNumber, 2)
        Title := Format("{}:{}      {}:{}", GetLang("å˜é‡å"), varName, GetLang("å˜é‡å€¼"), curValue)
        Title .= "`n" GetLang("è¯·è¾“å…¥æ–°çš„å˜é‡å€¼ï¼š")
        newValue := InputBox(Title, "ä¿®æ”¹", "w300 h110")

        ; æ£€æŸ¥ç”¨æˆ·æ˜¯å¦å–æ¶ˆè¾“å…¥
        if newValue.Result = "Cancel"
            return
        if (newValue.Value == "") {
            DelGlobalVariable([varName])
            return
        }
        SetGlobalVariable([varName], [newValue.Value], false)
    }
}
