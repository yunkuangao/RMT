#Requires AutoHotkey v2.0

class UseExplainGui {
    __new() {
        this.Gui := ""
        this.ContextMenu := ""
        this.AuthorCon := ""
        this.EffectCon := ""
        this.OperCon := ""
        this.LVCon := ""
        this.AllImagePathMap := Map()
        this.ImagePathArr := []
        this.CheckClipboardAction := () => this.CheckClipboard()
        this.SettingPath := ""
        this.Mode := 1  ;1æŸ¥çœ‹æ¨¡å¼  2ä¸Šä¼ ç¡®è®¤æ¨¡å¼
        this.HasChange := false
        this.ModeAction := ""
    }

    ShowGui(SettingPath) {
        if (this.Gui != "") {
            this.Gui.Show()
        }
        else {
            this.AddGui()
        }
        this.Init(SettingPath)
        this.LVCon.Focus()  ; ğŸ”¥ å¼ºåˆ¶è·å¾—ç„¦ç‚¹ï¼Œè§£å†³ç¬¬ä¸€æ¬¡åŒå‡»æ— æ•ˆé—®é¢˜
    }

    AddGui() {
        MyGui := Gui(, GetLang("ä½¿ç”¨è¯´æ˜"))
        this.Gui := MyGui
        MyGui.SetFont("S11 W550 Q2", MySoftData.FontType)

        PosX := 10
        PosY := 10
        MyGui.Add("Text", Format("x{} y{}", PosX, PosY), GetLang("é…ç½®åç§°ï¼š"))

        PosX := 100
        MyGui.Add("Text", Format("x{} y{}", PosX, PosY), MySoftData.CurSettingName)

        PosX := 10
        PosY += 30
        MyGui.Add("Text", Format("x{} y{}", PosX, PosY), GetLang("ä½œè€…ï¼š"))
        PosX := 100
        this.AuthorCon := MyGui.Add("Edit", Format("x{} y{} w{}", PosX, PosY - 2, 480))
        this.AuthorCon.OnEvent("Change", this.OnValueChange.Bind(this))

        PosX := 10
        PosY += 40
        MyGui.Add("Text", Format("x{} y{}", PosX, PosY), GetLang("é…ç½®ä½œç”¨ï¼š"))
        PosX := 100
        this.EffectCon := MyGui.Add("Edit", Format("x{} y{} w{} h{}", PosX, PosY - 2, 480, 60))
        this.EffectCon.OnEvent("Change", this.OnValueChange.Bind(this))

        PosX := 10
        PosY += 70
        MyGui.Add("Text", Format("x{} y{}", PosX, PosY), GetLang("æ“ä½œè¯´æ˜ï¼š"))
        PosX := 100
        this.OperCon := MyGui.Add("Edit", Format("x{} y{} w{} h{}", PosX, PosY - 2, 480, 200))
        this.OperCon.OnEvent("Change", this.OnValueChange.Bind(this))

        PosX := 10
        PosY += 210
        MyGui.Add("Text", Format("x{} y{}", PosX, PosY), GetLang("å›¾ç‰‡å¤‡æ³¨ï¼š"))
        PosX := 100
        this.LVCon := MyGui.AddListView(Format("x{} y{} w{} h{} Icon", PosX, PosY - 2, 480, 100))
        this.LVCon.OnEvent("DoubleClick", this.OnLVDoubleClick.Bind(this))
        this.LVCon.OnEvent("ContextMenu", this.OnLVRightClick.Bind(this))

        PosX := 10
        PosY += 25
        con := MyGui.Add("Button", Format("x{} y{} w{}", PosX, PosY, 80), GetLang("é€‰æ‹©å›¾ç‰‡"))
        con.OnEvent("Click", (*) => this.OnSelectImage())

        PosX := 10
        PosY += 35
        con := MyGui.Add("Button", Format("x{} y{} w{}", PosX, PosY, 80), GetLang("æˆªå›¾"))
        con.OnEvent("Click", (*) => this.OnScreenShot())

        PosY += 50
        PosX := 250
        btnCon := MyGui.Add("Button", Format("x{} y{} w{} h{}", PosX, PosY, 100, 40), GetLang("ç¡®å®š"))
        btnCon.OnEvent("Click", (*) => this.OnClickSureBtn())
        MyGui.OnEvent("Close", (*) => this.OnTriggerModeAction(false, false))
        MyGui.Show(Format("w{} h{}", 600, 520))
    }

    Init(SettingPath) {
        this.SettingPath := SettingPath
        this.IL := IL_Create(10, 5, true)   ; 5 = è‰²æ·±ï¼Œtrue = large icon
        this.LVCon.SetImageList(this.IL)
        this.ImagePathArr := []
        this.HasChange := false
        if (this.Mode == 2)
            this.Gui.Title := GetLang("è¯·å®Œå–„ä½¿ç”¨è¯´æ˜")
        OperFilePath := SettingPath "\ä½¿ç”¨è¯´æ˜&ç½²å.txt"
        IniSection := "Instructions for Use & Attribution"
        AuthorText := IniRead(OperFilePath, IniSection, "Author", "")
        EffectText := IniRead(OperFilePath, IniSection, "Effect", "")
        OperText := IniRead(OperFilePath, IniSection, "Operation", "")
        AuthorText := StrReplace(AuthorText, "â«¶", "`n")
        EffectText := StrReplace(EffectText, "â«¶", "`n")
        OperText := StrReplace(OperText, "â«¶", "`n")
        this.AuthorCon.Value := AuthorText
        this.EffectCon.Value := EffectText
        this.OperCon.Value := OperText

        this.LVCon.Delete()
        ImagesfolderPath := SettingPath "\Images\UseExplain"
        loop files ImagesfolderPath "\*.png" {
            this.AllImagePathMap.Set(A_LoopFileFullPath, true)
            this.ImagePathArr.Push(A_LoopFileFullPath)
            IL_Add(this.IL, this.ImagePathArr[A_Index])
            this.LVCon.Add("Icon" . A_Index)
        }
    }

    OnLVDoubleClick(LV, RowNumber, *) {
        if (RowNumber == 0)
            return
        this.OnValueChange()
        path := this.ImagePathArr[RowNumber]
        run path
    }

    OnLVRightClick(LV, RowNumber, isRightClick, x, y) {
        if (RowNumber == 0)
            return
        this.OnValueChange()
        this.RowNumber := RowNumber
        if (this.ContextMenu == "") {
            this.ContextMenu := Menu()
            this.ContextMenu.Add(GetLang("åˆ é™¤"), (*) => this.MenuHandler(GetLang("åˆ é™¤")))
        }
        this.ContextMenu.Show(x, y)
    }

    OnSelectImage() {
        path := FileSelect(1, , GetLang("é€‰æ‹©å›¾ç‰‡"), "PNG Files (*.png)")
        if (path == "")
            return

        this.OnValueChange()
        SplitPath path, &name, &dir, &ext, &name_no_ext, &drive
        newPath := this.SettingPath "\Images\UseExplain\" name
        if (FileExist(newPath)) {
            MsgBox(GetLang("è¯¥å›¾ç‰‡å·²æ·»åŠ ï¼Œè¯·å‹¿é‡å¤æ·»åŠ ï¼ï¼ï¼"))
            return
        }

        FileCopy(path, newPath)
        this.AllImagePathMap.Set(newPath, true)
        this.ImagePathArr.Push(newPath)
        IL_Add(this.IL, newPath)
        this.LVCon.Add("Icon" . this.AllImagePathMap.Count)
    }

    OnScreenShot() {
        this.OnValueChange()
        if (MySoftData.ScreenShotTypeCtrl.Value == 1) {
            A_Clipboard := ""  ; æ¸…ç©ºå‰ªè´´æ¿
            Run("ms-screenclip:")
            SetTimer(this.CheckClipboardAction, 500)  ; æ¯ 500 æ¯«ç§’æ£€æŸ¥ä¸€æ¬¡å‰ªè´´æ¿
        }
        else {
            TogSelectArea(true, this.OnScreenShotGetArea.Bind(this))
        }
    }

    CheckClipboard() {
        ; å¦‚æœå‰ªè´´æ¿ä¸­æœ‰å›¾åƒ
        if DllCall("IsClipboardFormatAvailable", "uint", 8)  ; 8 æ˜¯ CF_BITMAP æ ¼å¼
        {
            ; è·å–å½“å‰æ—¥æœŸå’Œæ—¶é—´ï¼Œç”¨äºç”Ÿæˆå”¯ä¸€çš„æ–‡ä»¶å
            CurrentDateTime := FormatTime(, "HHmmss")
            filePath := this.SettingPath "\Images\UseExplain\" CurrentDateTime ".png"
            SaveClipToBitmap(filePath)

            this.AllImagePathMap.Set(filePath, true)
            this.ImagePathArr.Push(filePath)
            IL_Add(this.IL, filePath)
            this.LVCon.Add("Icon" . this.AllImagePathMap.Count)
            SetTimer(, 0)
        }
    }

    OnScreenShotGetArea(x1, y1, x2, y2) {
        CurrentDateTime := FormatTime(, "HHmmss")
        filePath := this.SettingPath "\Images\UseExplain\" CurrentDateTime ".png"
        ScreenShot(x1, y1, x2, y2, filePath)

        this.AllImagePathMap.Set(filePath, true)
        this.ImagePathArr.Push(filePath)
        IL_Add(this.IL, filePath)
        this.LVCon.Add("Icon" . this.AllImagePathMap.Count)
    }

    CheckIfValid() {
        if (this.Mode == 1)
            return true

        if (Trim(this.AuthorCon.Value) == "") {
            MsgBox(GetLang("è¯·å®Œå–„ä½œè€…ä¿¡æ¯ï¼Œè‹¥ä¸æƒ³ç•™åè¯·è¾“å…¥åŒ¿å"))
            return false
        }

        if (Trim(this.EffectCon.Value) == "") {
            MsgBox(GetLang("è¯·å®Œå–„é…ç½®ä½œç”¨ä¿¡æ¯ï¼Œç®€è¦çš„ä»‹ç»é…ç½®çš„ä½œç”¨"))
            return false
        }

        if (Trim(this.OperCon.Value) == "") {
            MsgBox(GetLang("è¯·å®Œå–„æ“ä½œè¯´æ˜ä¿¡æ¯ï¼Œè¯¦ç»†è¯´æ˜é…ç½®å¯¹åº”çš„æ“ä½œ"))
            return false
        }

        return true
    }

    OnValueChange(*) {
        this.HasChange := true
    }

    OnTriggerModeAction(isSure, isChange) {
        if (this.ModeAction == "")
            return
        action := this.ModeAction
        action(isSure, isChange)
        this.ModeAction := ""
    }

    OnClickSureBtn() {
        isValid := this.CheckIfValid()
        if (!isValid)
            return

        OperFilePath := this.SettingPath "\ä½¿ç”¨è¯´æ˜&ç½²å.txt"
        IniSection := "Instructions for Use & Attribution"
        AuthorText := StrReplace(this.AuthorCon.Value, "`n", "â«¶")
        EffectText := StrReplace(this.EffectCon.Value, "`n", "â«¶")
        OperText := StrReplace(this.OperCon.Value, "`n", "â«¶")
        IniWrite(AuthorText, OperFilePath, IniSection, "Author")
        IniWrite(EffectText, OperFilePath, IniSection, "Effect")
        IniWrite(OperText, OperFilePath, IniSection, "Operation")
        this.Gui.Hide()
    
        this.OnTriggerModeAction(true, this.HasChange)
    }

    MenuHandler(cmdStr, *) {
        switch cmdStr {
            case GetLang("åˆ é™¤"):
            {
                imagePath := this.ImagePathArr[this.RowNumber]
                this.LVCon.Delete(this.RowNumber)
                this.ImagePathArr.RemoveAt(this.RowNumber)
                if (FileExist(imagePath))
                    FileDelete(imagePath)
            }
        }

    }
}
